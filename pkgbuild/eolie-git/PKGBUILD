# Maintainer: Bruno Miguel <brunoalexandremiguel@gmail.com>

pkgname=eolie-git
_gitname=eolie
pkgver=0.9.22.r687.g041bad8
pkgrel=1
pkgdesc="Simple GTK web browser for GNOME"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/Eolie"
license=('GPL3')
depends=('gtkspell3' 'python-cairo' 'python-dateutil' 'python-gobject' 'webkit2gtk')
makedepends=('git' 'gobject-introspection' 'meson')
optdepends=('python-beautifulsoup4: Import html bookmarks'
            'python-crypto: Firefox Sync support' 
            'python-fxa: Firefox Sync support'
            'python-pyopenssl: Show SSL certificates'
            'python-requests-hawk: Firefox Sync support')
source=("git+https://gitlab.gnome.org/World/eolie.git")
sha256sums=('SKIP')

pkgver() {
	cd "${_gitname}"
	( set -o pipefail
		git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build() {
	arch-meson "${_gitname}" build
	ninja -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}

